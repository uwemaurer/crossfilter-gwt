buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

repositories {
        jcenter()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'distribution'
apply plugin: 'gwt'



sourceCompatibility = 1.8
version = '1.1.1'

eclipse {
	classpath {
		defaultOutputDir = file('war/WEB-INF/classes')
	}
}

sourceSets {
	main {
		java.srcDirs = ['src']
	}
	demo {
		java.srcDirs = ['srctest']
	}
} 

configurations {
	gwtJar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.main.output
}

assemble.dependsOn(sourcesJar)

artifacts {
    gwtJar sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

gwt {
    gwtVersion='2.8.0'

    src = files(sourceSets.demo.java.srcDirs) + files(sourceSets.main.java.srcDirs)
    	 
    modules 'com.crossfilter.demo.CrossfilterDemo'
    minHeapSize = "512M"
    maxHeapSize = "1024M"
    eclipse {
    	addGwtContainer = false
    }
}

task copyGwtToWar(dependsOn: compileGwt) {
  doLast {
		def dir = "war"
		delete files("$dir/crossfilter-demo")
		copy {
			from tasks.compileGwt.war
			into dir
		}
	}
}

compileGwt.finalizedBy copyGwtToWar
